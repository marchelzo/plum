import buffer
import editor
import window
import proc
import plum::command as command

/* add some commands */
import commands

let indent = 4;

/* respond to Command messages */
buffer::onMessage('Command', (_, cmd) -> command::run(cmd));

/* Special buffer IDs */
let inputBuffer = 2;
let openBuffer = 3;

if (buffer::id() == 1) {
        /* initialize the special buffers */
        buffer::new('plum::input');
        buffer::new('plum::open');
}

function delCurrent() {
        buffer::startOfLine();
        buffer::cutLine();
        if (buffer::line() == buffer::lines() - 1) {
                buffer::backward(1);
        }

        buffer::remove(1);

        buffer::startOfLine();
}

buffer::mapInsert(['Enter'], function () {
        let line = buffer::getLine();
        let currentIndent = line.chars().takeWhile!(|# == ' '|).len();
        buffer::insert("\n");
        for (let i = 0; i < currentIndent; ++i)
                buffer::insert(' ');
});

buffer::mapInsert(['C-e'],       buffer::endOfLine);
buffer::mapInsert(['C-a'],       buffer::startOfLine);
buffer::mapInsert(['C-n'],       buffer::nextLine);
buffer::mapInsert(['C-p'],       buffer::prevLine);
buffer::mapInsert(['C-k'],       buffer::cutLine);
buffer::mapInsert(['j', 'k'],    buffer::normalMode);

buffer::mapInsert(['M-Left'],  window::left);
buffer::mapInsert(['M-Right'], window::right);
buffer::mapInsert(['M-Up'],    window::up);
buffer::mapInsert(['M-Down'],  window::down);
buffer::mapNormal(['M-Left'],  window::left);
buffer::mapNormal(['M-Right'], window::right);
buffer::mapNormal(['M-Up'],    window::up);
buffer::mapNormal(['M-Down'],  window::down);

buffer::mapInsert(['S-Left'],  () -> window::growHorizontally(-2));
buffer::mapInsert(['S-Right'], () -> window::growHorizontally(2));
buffer::mapInsert(['S-Up'],    () -> window::growVertically(2));
buffer::mapInsert(['S-Down'],  () -> window::growVertically(-2));
buffer::mapNormal(['S-Left'],  () -> window::growHorizontally(-2));
buffer::mapNormal(['S-Right'], () -> window::growHorizontally(2));
buffer::mapNormal(['S-Up'],    () -> window::growVertically(2));
buffer::mapNormal(['S-Down'],  () -> window::growVertically(-2));

buffer::mapInsert(['Backspace'], function () {
        let spc = buffer::getLine().chars().take!(buffer::column()).reverse!().takeWhile!(|# == ' '|).len();
        let remove = min(indent, max(1, spc));
        if (buffer::backward(remove) == remove)
                buffer::remove(remove);
});

buffer::mapNormal(['i'], buffer::insertMode);
buffer::mapNormal(['a'], function () {
        buffer::moveRight(1);
        buffer::insertMode();
});

buffer::mapNormal(['C-w', '+'], |window::growVertically(4)|);
buffer::mapNormal(['C-w', '-'], |window::growVertically(-4)|);

buffer::mapNormal(['C-w', '>'], |window::growHorizontally(4)|);
buffer::mapNormal(['C-w', '<'], |window::growHorizontally(-4)|);

buffer::mapNormal(['C-w', 'C-w'], window::next);

buffer::mapNormal(['j'], buffer::nextLine);
buffer::mapNormal(['k'], buffer::prevLine);

buffer::mapNormal(['g', 'g'], buffer::gotoStart);

buffer::mapNormal(['G'], function () { buffer::gotoEnd(); buffer::startOfLine(); });

buffer::mapNormal(['I'], function () {
        buffer::startOfLine();
        while (buffer::getChar() == ' ')
                buffer::moveRight(1);
        buffer::insertMode();
});

buffer::mapNormal(['A'], function () {
        buffer::endOfLine();
        buffer::insertMode();
});

buffer::mapNormal(['o'], function () {
        buffer::endOfLine();
        buffer::insert("\n");
        buffer::insertMode();
});

buffer::mapNormal(['O'], function () {
        buffer::startOfLine();
        buffer::insert("\n");
        buffer::prevLine();
        buffer::insertMode();
});

buffer::mapNormal(['0'], function () {
        buffer::startOfLine();
});

buffer::mapNormal(['$'], function () {
        buffer::endOfLine();
});

buffer::mapNormal(['x'], function () {
        if (buffer::getChar() != "\n")
                buffer::remove(1);
});

buffer::mapNormal(['h'], function () {
        buffer::moveLeft(1);
});

buffer::mapNormal(['l'], function () {
        buffer::moveRight(1);
});

buffer::mapNormal(['>', '>'], function () {
        let i = buffer::column();
        buffer::startOfLine();
        for (let i = 0; i < indent; ++i)
                buffer::insert(' ');
        buffer::forward(i);
});

buffer::mapNormal(['<', '<'], function () {
        let i = buffer::column();
        buffer::startOfLine();
        let l = buffer::getLine();
        let n = min(indent, l.chars().takeWhile!(/\s/).len());
        buffer::remove(n);
        buffer::forward(max(i - n, 0));
});

buffer::mapNormal(['w'], function () {
        let rest = buffer::getLine().slice(buffer::column()) || '';
        if let i = rest.search(/\s\S/) {
                buffer::moveRight(i + 1);
        }
});

buffer::mapNormal(['b'], function () {
        buffer::moveLeft();

        let n = 0;
        while (buffer::getChar() != ' ' && buffer::moveLeft() == 1)
                ++n;

        if (n <= 1) {
                while (buffer::getChar() == ' ' && buffer::moveLeft() == 1);
                while (buffer::getChar() != ' ' && buffer::moveLeft() == 1);
        }

        if (buffer::getChar() == ' ' && buffer::column() != 0)
                buffer::moveRight();
});

buffer::mapNormal(['^'], function () {
        let cs = buffer::getLine().chars();
        let i = cs.takeWhile!(|#.match?(/\s/)|)
                  .len();
        buffer::startOfLine();
        buffer::forward(i);
});

buffer::mapNormal(['C'], function () {
        buffer::cutLine();
        buffer::insertMode();
});

buffer::mapNormal(['d', 'd'], delCurrent);

buffer::mapInsert(['Tab'], function () { buffer::insert([1 .. (indent - (buffer::column() % indent))].map(|' '|).sum()); });

buffer::mapNormal(['u'], buffer::undo);

buffer::mapNormal(['C-r'], buffer::redo);

buffer::mapNormal(['z', 'z'], buffer::centerCurrentLine);
buffer::mapNormal(['d', 'G'], buffer::clearToEnd);
buffer::mapNormal(['d', 'g', 'g'], buffer::clearToStart);
buffer::mapNormal(['s'], function () { if (buffer::getChar() != "\n") buffer::remove(1); buffer::insertMode(); });
buffer::mapNormal(['D'], buffer::cutLine);

buffer::mapNormal(['F12'], editor::showConsole);

buffer::mapNormal(['C-w', 'C-v'], window::horizontalSplit);
buffer::mapNormal(['C-w', 'C-n'], window::verticalSplit);
buffer::mapNormal(['C-w', 'd'], window::delete);
buffer::mapNormal(['C-w', 'C-c'], window::cycleColor);

buffer::mapNormal([';'], function () {
        let win = window::verticalSplit(inputBuffer, 1);
        buffer::sendMessage(inputBuffer, 'Start', str(window::id()));
        window::goto(win);
});

buffer::mapNormal(['C-p'], function () {
        let win = window::verticalSplit(openBuffer, 1);
        buffer::sendMessage(openBuffer, 'Start', str(window::id()));
        window::goto(win);
});

buffer::mapNormal(['C-g'], function () {
        editor::echo("File: {buffer::fileName()}    Line: {buffer::line() + 1}    Column: {buffer::column() + 1}");
});
